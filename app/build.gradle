apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

Properties props = new Properties()
props.load(new FileInputStream(file("../config/signing.properties")))
props.load(new FileInputStream(file("../config/key.properties")))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.jude.fishing"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 53
        versionName "1.1.3"
        multiDexEnabled true
        manifestPlaceholders = [
                UMENG_APPKEY    : props['UMENG_APPKEY'],
                AMAP_APPKEY     : props['AMAP_APPKEY'],
                UMENG_SECRET    : props['UMENG_SECRET'],
                UMENG_MESSAGE_SECRET    : props['UMENG_MESSAGE_SECRET'],
                RONGYUN_APPKEY  : props['RONGYUN_APPKEY'],
                SMS_MOB_APPKEY    : props['SMS_MOB_APPKEY'],
                SMS_MOB_APPSECRET    : props['SMS_MOB_APPSECRET'],
        ]
    }
    productFlavors {
        xiaomi{}
        baidu{}
        wandoujia{}
        qihu360{}
        share{}
        liuhengyu{}
        origin{}
        update{}
        qq{}
    }
    //应用到每一个flavor
    productFlavors.all { flavor ->
        manifestPlaceholders.put("UMENG_CHANNEL",name)
    }

    signingConfigs {
        release{
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.outputFile != null && output.outputFile.name.endsWith('.apk')
                            &&'release'.equals(variant.buildType.name)) {
                        def apkFile = new File(
                                output.outputFile.getParent(),
                                "Fashing_${variant.flavorName}_v${variant.versionName}_${buildTime()}.apk")
                        output.outputFile = apkFile
                    }
                }
            }
            ndk {
                abiFilters "armeabi"
            }
        }
        debug {
            signingConfig signingConfigs.release
            ndk {
                abiFilters "armeabi"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/maven/com.squareup.okio/okio/pom.xml'
        exclude 'META-INF/maven/com.squareup.okio/okio/pom.properties'
    }
}

def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.facebook.fresco:fresco:0.8.1'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.qiniu:qiniu-android-sdk:7.0.7'
    compile 'com.jude:beam:2.8.1-alpha2'
    compile 'com.jude:swipebackhelper:2.1.3'
    compile 'com.jude:imageprovider:2.0.2'
    compile 'com.jude:utils:1.1.1'
    compile 'com.jude:smssdk_mob:1.0.3'
    compile 'com.jude:fitsystemwindowlayout:1.0.8'
    compile project(':umengfamily')
    compile project(':IMKit')
    compile files('libs/Android_Location_V1.3.2.jar')
    compile 'com.jude:tagview:2.1.3'
    compile 'com.github.clans:fab:1.6.0'
    compile 'com.jude:exgridview:1.1.5'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.jude:rollviewpager:1.0.4'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
    compile 'com.squareup.sqlbrite:sqlbrite:0.4.0'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.+'
    compile('com.afollestad.material-dialogs:core:0.8.2.0@aar') {
        transitive = true
    }
    compile files('libs/AMap_Search_v2.6.0_20150914.jar')
    compile files('libs/AMap_Navi_v1.4.0_20150916.jar')
    compile files('libs/Msc.jar')
    compile 'com.pnikosis:materialish-progress:1.7'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta1'
    // or 1.4-beta1
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
    // or 1.4-beta1
    compile files('libs/AMap_3DMap_V3.3.0_20160331.jar')
    compile files('libs/AMap_Location_V2.4.0_20160308.jar')
}
