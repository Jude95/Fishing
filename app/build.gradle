apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

Properties props = new Properties()
props.load(new FileInputStream(file("../config/signing.properties")))
props.load(new FileInputStream(file("../config/key.properties")))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.jude.fishing"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 42
        versionName "1.0.2"
        multiDexEnabled true
        manifestPlaceholders = [
                UMENG_APPKEY    : props['UMENG_APPKEY'],
                AMAP_APPKEY     : props['AMAP_APPKEY'],
                UMENG_SECRET    : props['UMENG_SECRET'],
                RONGYUN_APPKEY  : props['RONGYUN_APPKEY']
        ]
    }

    productFlavors {
        xiaomi{}
        baidu{}
        wandoujia{}
        qihu360{}
        share{}
        liuhengyu{}
        origin{}
    }
    //应用到每一个flavor
    productFlavors.all { flavor ->
        manifestPlaceholders.put("UMENG_CHANNEL",name)
    }

    signingConfigs {
        release{
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.outputFile != null && output.outputFile.name.endsWith('.apk')
                            &&'release'.equals(variant.buildType.name)) {
                        def apkFile = new File(
                                output.outputFile.getParent(),
                                "Fashing_${variant.flavorName}_v${variant.versionName}_${buildTime()}.apk")
                        output.outputFile = apkFile
                    }
                }
            }
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.facebook.fresco:fresco:0.7.0+'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.qiniu:qiniu-android-sdk:7.0.7'
    compile 'com.jude:beam:2.4.7'
    compile 'com.jude:swipebackhelper:2.1.3'
    compile 'com.jude:imageprovider:2.0.2'
    compile 'com.jude:utils:1.0.7'
    compile project(':umengfamily')
    compile project(':IMKit')
    compile files('libs/Android_Location_V1.3.2.jar')
    compile 'com.jude:tagview:2.1.3'
    compile 'com.github.clans:fab:1.6.0'
    compile project(':SMSSDK')
    compile 'com.jude:exgridview:1.1.5'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.jude:rollviewpager:1.0.4'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile files('libs/AMap_2DMap_v2.6.0_20150916.jar')
    compile 'com.squareup.sqlbrite:sqlbrite:0.4.0'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.+'
    compile('com.afollestad.material-dialogs:core:0.8.2.0@aar') {
        transitive = true
    }
    compile files('libs/AMap_Search_v2.6.0_20150914.jar')
    compile files('libs/AMap_Navi_v1.4.0_20150916.jar')
    compile files('libs/AMap_Location_v1.4.1_20150917.jar')
    compile files('libs/AMap_3DMap_v2.6.0_20150914.jar')
    compile files('libs/Msc.jar')
    compile 'com.pnikosis:materialish-progress:1.7'
}
